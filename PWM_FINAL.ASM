#INCLUDE <P16F628A.INC>
	    
; Configuração dos Fuses bits do microcontrolador
    __CONFIG _FOSC_INTOSCIO & _WDTE_OFF & _PWRTE_ON & _MCLRE_OFF & _BOREN_OFF & _LVP_OFF & _CPD_OFF & _CP_OFF
    
    
; Configuração PWM 
; PR2 = [(Fosc) / (Fpwm x 4 x PRESCALE_TIMER2)] - 1]
; PR2 = [(4Mhz) / (10Khz x 4 x 1)] - 1 = 99
    
; DutyCycle(%) x (PR2 + 1) x 4 = CCPR1L:CCP1CON<4:5> (resultado em decimal -->transformar em binário)
; 0.25 x (100) x 4 = 0001 1001 00
; 0.5 x (100) x 4 = 0011 0010 00
; 1   x (100) x 4 = 1100 1000 00
; Para duty(25%) --> CCP1RL = 0001 1001
; Para duty(50%) --> CCP1RL = 0011 0010
; Para duty(100%) --> CCP1RL = 1100 1000
    
#DEFINE SENSOR_ESQUERDO	    PORTB,  5
#DEFINE	SENSOR_MEIO	    PORTB,  6
#DEFINE	SENSOR_DIREITO	    PORTB,  7
      
; ESTADOS DE PORTA TAL QUE OCORREM AS SEGUINTES AÇÕES
; ESSES ESTADOS SERÃO INSERIDOS EM PORTA
    
#DEFINE	VIRA_ESQUERDA		b'00000110'	;DEFINE O BYTE DE TAL FORMA QUE O MOTOR DIREITO SEJA ACIONADO
#DEFINE VIRA_ESQUERDA_FRACO	b'00000010'
#DEFINE	FRENTE			b'00001010'	;DEFINE O BYTE DE TAL FORMA QUE AMBOS OS MOTORES SEJAM ACIONADOS
#DEFINE	VIRA_DIREITA		b'00001001'	;DEFINE O BYTE DE TAL FORMA QUE O MOTOR ESQUERDO SEJA ACIONADO
#DEFINE VIRA_DIREITA_FRACO	b'00001000'
#DEFINE TRAS			b'00000101'
    
    
; PINAGEM DA PONTE H
;#DEFINE	INPUT_1		PORTA,	0
;#DEFINE	INPUT_2		PORTA,	1
;#DEFINE	INPUT_3		PORTA,	2
;#DEFINE	INPUT_4		PORTA,	3
    
; FRENTE --> 
	    ;INPUT_1 = 1, MOTOR E FRENTE
	    ;INPUT_2 = 0, 
	    ;INPUT_3 = 1, MOTOR D FRENTE
	    ;INPUT_4 = 0
    
; DIREITA --> 
	    ;INPUT_1 = 1, MOTOR E FRENTE
	    ;INPUT_2 = 0, 
	    ;INPUT_3 = 0, MOTOR D TRÁS
	    ;INPUT_4 = 1
	    
; DIREITA FRACO --> 
	    ;INPUT_1 = 1, MOTOR E FRENTE
	    ;INPUT_2 = 0, 
	    ;INPUT_3 = 0, MOTOR D PARADO
	    ;INPUT_4 = 0
    
; ESQUERDA -->	
	    ;INPUT_1 = 0, MOTOR E TRÁS
	    ;INPUT_2 = 1, 
	    ;INPUT_3 = 1, MOTOR D FRENTE
	    ;INPUT_4 = 0

; ESQUERDA FRACO -->	
	    ;INPUT_1 = 0, MOTOR E PARADO
	    ;INPUT_2 = 0, 
	    ;INPUT_3 = 1, MOTOR D FRENTE
	    ;INPUT_4 = 0

    ORG 0x00
    GOTO INICIO

    ORG 0x04
    RETFIE

INICIO:
    BANKSEL TRISA
    CLRF    TRISA
    
    BANKSEL PORTA
    
    CLRF    PORTA
    
    BANKSEL TRISB
    
    MOVLW   b'11110111'
    MOVWF   TRISB
    
PWM:
    
    BANKSEL PR2
    MOVLW   d'99'
    MOVWF   PR2
    
    BANKSEL CCPR1L
    MOVLW   b'00110010'
    MOVWF   CCPR1L
    
    BANKSEL CCP1CON
    MOVLW   b'00001111'
    MOVWF   CCP1CON
    
    BANKSEL T2CON
    MOVLW   b'00000100'
    MOVWF   T2CON
    
    BANKSEL PORTB

LOOP:
    BTFSC   SENSOR_MEIO
    CALL    MEIO
    
    BTFSC   SENSOR_ESQUERDO
    CALL    ESQUERDA
    
    BTFSC   SENSOR_DIREITO
    CALL    DIREITA
    
    CALL    FULL
    CALL    NONE

ESCREVE:
    BANKSEL PORTA
    MOVWF   PORTA
 
    GOTO    LOOP
    
MEIO	
	BTFSC	SENSOR_ESQUERDO
	RETURN
	BTFSC	SENSOR_DIREITO
	RETURN
	CALL	DUTY_100
	MOVLW	FRENTE
	RETURN
	
FULL	
	BTFSS	SENSOR_MEIO
	RETURN
	BTFSS	SENSOR_ESQUERDO
	RETURN
	BTFSS	SENSOR_DIREITO
	RETURN
	CALL	DUTY_100
	MOVLW	FRENTE
	RETURN
	
ESQUERDA
	BTFSC	SENSOR_MEIO
	CALL	DUTY_25
	BTFSS	SENSOR_MEIO
	CALL	DUTY_50
	MOVLW	VIRA_ESQUERDA
	RETURN
    
DIREITA	
	BTFSC	SENSOR_MEIO
	CALL	DUTY_25
	BTFSS	SENSOR_MEIO
	CALL	DUTY_50
	MOVLW	VIRA_DIREITA
	RETURN		

NONE
	BTFSC	SENSOR_MEIO
	RETURN
	BTFSC	SENSOR_ESQUERDO
	RETURN
	BTFSC	SENSOR_DIREITO
	RETURN
	CALL	DUTY_100
	MOVLW	TRAS
	RETURN
	
DUTY_25
	; Duty Cycle (25%)
	BANKSEL CCPR1L
	MOVLW	B'00011001'
	MOVWF	CCPR1L
	RETURN
	
DUTY_50
	; Duty Cycle (50%)
	BANKSEL	CCPR1L
	MOVLW	B'00110010'
	MOVWF	CCPR1L
	RETURN
	
DUTY_100
	; Duty Cycle (100%)
	BANKSEL CCPR1L
	MOVLW	B'11001000'
	MOVWF	CCPR1L
	RETURN
	
    END
